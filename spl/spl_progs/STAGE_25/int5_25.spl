alias userSP R1;
alias systemCallNum R2;
alias processTableEntryAddress R3;

processTableEntryAddress = PROCESS_TABLE + [SYSTEM_STATUS_TABLE+1]*16;
[processTableEntryAddress + 13]= SP;
SP = [processTableEntryAddress + 11] * 512  - 1 ;
PTBR = [processTableEntryAddress + 14] ;

userSP = [processTableEntryAddress + 13];
systemCallNum = [[PTBR + 2*((userSP-5)/512)] * 512 + ((userSP-5)%512)] ;
[processTableEntryAddress + 9] = systemCallNum;
alias arg1 R4;
arg1  = [[PTBR + 2*((userSP-4)/512)] * 512 + ((userSP-4)%512)] ;
alias returnAddress R5;
returnAddress = [PTBR + 2*((userSP-1)/512)] * 512 + ((userSP-1)%512) ;

if(systemCallNum == 2) then //  Open
    alias fileName R4;

    //  Find a free Per-Process Resource Table entry.
    alias perProcessResouceTable R6;
    alias freeEntry R7;
    alias iter R8;
    perProcessResouceTable = [processTableEntryAddress + 11] * 512 + RESOURCE_TABLE_OFFSET;
    iter = 0;
    freeEntry = -1;

    // The Per-Process Resource Table has 8 entries and each entry is of 2 words. The last 16 words of the User Area Page are reserved for this.
    // The per-process resource table entry has the following format. Resource Identifier (1 word)	Index of Open File Table/ Semaphore Table entry (1 word)
    // A free entry is denoted by -1 in the Resource Identifier field.

    while(iter < 8) do 
        if([perProcessResouceTable + 2*iter] == -1) then
            freeEntry = iter;
            break;
        endif;
        iter = iter + 1;

    endwhile;

    // If there is no free entry, return -3.
    if(freeEntry == -1) then
        //  return to the user mode with -3 stored as return value indicating Process has reached its limit of resources.
            [returnAddress] = -3;

            // At any point of return to user, remember to reset the MODE FLAG and change the stack to user stack.
            [processTableEntryAddress + 9] = 0;
            SP = [processTableEntryAddress + 13];
            ireturn;
    endif;


    // Call the open() function from the File Manager module to get the Open File table entry.
    multipush(R1,R2,R3,R4,R5,R6,R7);
    R1 = OPEN;
    R2 = fileName;
    call FILE_MANAGER;
    multipop(R1,R2,R3,R4,R5,R6,R7);

    // If Open fails, return the error code.
    if(R0 == -1 || R0 == -2 || R0 == -3) then
            //  return to the user mode with the error code stored as return value.
            [returnAddress] = R0;

            // At any point of return to user, remember to reset the MODE FLAG and change the stack to user stack.
            [processTableEntryAddress + 9] = 0;
            SP = [processTableEntryAddress + 13];
            ireturn;
    endif;


    // Set the Per-Process Resource Table entry
    [perProcessResouceTable + 2*freeEntry] = FILE;
    [perProcessResouceTable + 2*freeEntry + 1] = R0;


    // Return the index of the Per-Process Resource Table entry.   ( success )
    // The index of this entry is the File Descriptor of the file.
    [returnAddress] = freeEntry;
endif;



if(systemCallNum == 6 || systemCallNum ==  3) then // seek or close
    alias fileDescriptor R4;
    if(fileDescriptor<0 || fileDescriptor>7) then
        [returnAddress] = -1;
        [processTableEntryAddress + 9] = 0;
        SP = [processTableEntryAddress + 13] ;
        ireturn;
    endif;
	
    alias perProcessResouceTableEntry R6;
    perProcessResouceTableEntry = [processTableEntryAddress + 11]  * 512  + RESOURCE_TABLE_OFFSET + 2*fileDescriptor;
    if([perProcessResouceTableEntry] == -1 || [perProcessResouceTableEntry] != FILE) then
        [returnAddress] = -1;
        [processTableEntryAddress + 9] = 0;
        SP = [processTableEntryAddress + 13] ;
        ireturn;
    endif;

    alias openFileTableEntry R7;
    openFileTableEntry = OPEN_FILE_TABLE + [perProcessResouceTableEntry + 1] * 4;
    
    if(systemCallNum == 6) then
       
        alias inodeIndexOfFile R8;
        alias inodeTableEntryAddress R9;
        inodeIndexOfFile = [openFileTableEntry ];

        inodeTableEntryAddress =  INODE_TABLE + inodeIndexOfFile * 16 ;

        if([inodeTableEntryAddress+4] == 0) then
            if( [SYSTEM_STATUS_TABLE] != 1 && [inodeTableEntryAddress+3] != [SYSTEM_STATUS_TABLE] && [inodeTableEntryAddress+4] == EXCLUSIVE) then
                [returnAddress] = -3;
                [processTableEntryAddress + 9] = 0;
                SP = [processTableEntryAddress + 13];
                ireturn;
            endif;
        endif;

        multipush(R1,R2,R3,R4,R5,R6,R7,R8,R9);
        R1 = ACQUIRE_INODE;
        R2 = inodeIndexOfFile;
        R3 = [SYSTEM_STATUS_TABLE+1];
        call RESOURCE_MANAGER;
        multipop(R1,R2,R3,R4,R5,R6,R7, R8,R9);

        if(R0 == -1) then
            [returnAddress] = -1;
            [processTableEntryAddress + 9] = 0;
            SP = [processTableEntryAddress + 13] ;

            ireturn;
        endif;

        alias offset R10;
        offset = [([PTBR + 2* ((userSP - 3) / 512)] * 512) + ((userSP - 3) % 512)] ;
    
        alias fileSize R11;
        if(inodeIndexOfFile == INODE_ROOT) then
            fileSize = 480;
        else
            fileSize = [inodeTableEntryAddress + 2];
        endif;


        if([openFileTableEntry+2] + offset < 0 ) then 

            //  Release the Lock on the File by calling the release_inode() function in the Resource Manager module.
            // calling RELEASE_INODE
            multipush(R1,R2,R3,R4,R5,R6,R7,R8, R9,R10,R11);
            R1 = RELEASE_INODE;
            R2 = inodeIndexOfFile;
            R3 = [SYSTEM_STATUS_TABLE+1];
            call RESOURCE_MANAGER;
            multipop(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);


            [returnAddress] = -2;
            // setting the mode flag back to zero
            [processTableEntryAddress + 9] = 0;
            // Change SP back to user stack and return to the user mode.
            SP = [processTableEntryAddress + 13] ;
            ireturn;

        endif;
        
        // If the given offset is 0,
        // Set lseek value in the Open File Table entry to 0.
        if(offset == 0) then
            [openFileTableEntry+2] = 0;

        else
            if([openFileTableEntry+2]+offset > fileSize) then
                // if lseek+offset is greater than the file size,
                // Set the lseek value to file size.
                [openFileTableEntry+2] = fileSize;

            else
                // if  both of the above condtion is not true
                // Change the lseek value in the Per-Process Resource Table entry to lseek+offset.
                [openFileTableEntry+2] = [openFileTableEntry+2] + offset;

            endif;
        endif;



        //  Release the Lock on the File by calling the release_inode() function in the Resource Manager module.
        // calling RELEASE_INODE
        multipush(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);
        R1 = RELEASE_INODE;
        R2 = inodeIndexOfFile;
        R3 = [SYSTEM_STATUS_TABLE+1];
        call RESOURCE_MANAGER;
        multipop(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);
    endif;

    if(systemCallNum == 3) then // Close
        // Get the index of the Open File Table entry from Per-Process Resource Table entry. ([perProcessResouceTableEntry+1])
        multipush(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);
        R1 = CLOSE;
        R2 = [perProcessResouceTableEntry+1];
        call FILE_MANAGER;
        multipop(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);


        // Invalidate the Per-Process Resource Table entry.
        [perProcessResouceTableEntry] = -1;
        [perProcessResouceTableEntry+1] = -1;
    endif;


    //  return to the user mode with 0 stored as return value indicating success.
    [returnAddress] = 0;
endif;


// At any point of return to user, remember to reset the MODE FLAG and change the stack to user stack.
[processTableEntryAddress + 9] = 0;
SP = [processTableEntryAddress + 13];
ireturn;
